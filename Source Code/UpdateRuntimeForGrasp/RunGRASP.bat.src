@echo off
setlocal 

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

REM 
REM RunGRASP.bat
REM DOS script used to launch the GRASP GUI and associated processes.
REM See GitHub for futher documentation.
REM Copyright (r) 2016, 2017 PsyPhy Consulting.
REM

REM One can select a different path to the results file by editing the next line.
REM Default it relative to where this file is executing.
REM set GRASP_RESULTS_PATH="Results"
REM Here we move it elsewhere.
set GRASP_RESULTS_PATH="C:\DATA\GraspResults"

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

REM If OculusMouse is already running, kill it so that we don't have multiple copies.
TaskKill /F /IM OculusMouse*

REM Same for the CODA daemon.
TaskKill /IM GraspTrackerDaemon.exe

REM If one of the processes is not already running, TaskKill will generate an error message.
REM This could cause some consternation, even though it is a normal occurance.
REM So clear the console window to hide any message about processes that are not present.
cls

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

if exist NoHMD.flg goto NOHMD

REM The Oculus runtime process may not start up until we try to access the Oculus from within a program.
REM That could be annoyingly too late. So we launch a small process just to get the Oculus runtime to 
REM start up and we take the same opportunity to minimize the window so that it is not a distraction.
Start "Initializing Oculus" /MIN /WAIT Executables\StartOculus.exe 

REM StartOculus can take some time. Furthermore, it can stall with the Health & Safety warning.
REM OculusStartupCheck.exe is designed to check that the Oculus will indeed run and to prompt the 
REM subject on what to do in case it does to start up correctly.
Start "Wait for Oculus" /WAIT Executables\OculusStartupCheck.exe 
if NOT ERRORLEVEL 0 GOTO :EOF

REM Now start up OculuseMouse to be able to move the mouse pointer and click using the Oculus Remote.
REM When GRASP exits, OculusMouse will still be running so that if the exit was accidental
REM  the subject can use the Remote to navigate on the computer and restart.
Start "OculusMouse" /b Executables\OculusMouse.exe 

:NOHMD

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

if exist NoCODA.flg goto NOCODA

REM Start up the CODA system and wait for it to be active. The Daemon window will minimize automatically.
Start "GRASP Tracker" Executables\GraspTrackerDaemon.exe --hide

REM Don't proceed until output from the tracker daemon has been detected.
Start "Wait For Tracker" /MIN /WAIT Executables\WaitForCodaDaemon.exe  
if NOT ERRORLEVEL 0 GOTO :EOF

:NOCODA

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

REM Make sure that all of the windows are closed.
Executables\MinimizeAll

REM Here we actually start the Grasp user interface.
Start "Grasp Console Window" Executables\GraspGUI.exe %GRASP_RESULTS_PATH%



